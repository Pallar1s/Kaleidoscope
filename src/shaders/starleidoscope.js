export const starleidoscopeFragmentShader = `
  precision highp float;
  
  uniform float u_time;
  uniform vec2 u_resolution;
  
  #define NUM_LAYERS 10.0
  
  mat2 Rot(float a) {
    float c = cos(a), s = sin(a);
    return mat2(c, -s, s, c);
  }

  float Star(vec2 uv, float flare) {
    float col = 0.0;
    float d = length(uv);
    float m = 0.02 / d;
    
    float rays = max(0.0, 1.0 - abs(uv.x * uv.y * 1000.0));
    m += rays * flare;
    uv *= Rot(3.1415 / 4.0);
    rays = max(0.0, 1.0 - abs(uv.x * uv.y * 1000.0));
    m += rays * 0.3 * flare;
    
    m *= smoothstep(1.0, 0.2, d);

    return m;
  }

  float Hash21(vec2 p) {
    p = fract(p * vec2(123.34, 456.21));
    p += dot(p, p + 45.32);
    return fract(p.x * p.y);
  }

  vec3 StarLayer(vec2 uv) {
    vec3 col = vec3(0.0);
    
    vec2 gv = fract(uv) - 0.5;
    vec2 id = floor(uv);
    
    for (int y = -1; y <= 1; y++) {
      for (int x = -1; x <= 1; x++) {
        vec2 offs = vec2(float(x), float(y));

        float n = Hash21(id + offs);
        float size = fract(n * 345.32);
        
        vec2 p = vec2(n, fract(n * 34.0));
        
        float star = Star(gv - offs - p + 0.5, smoothstep(0.8, 1.0, size) * 0.6);
        
        float hueShift = fract(n * 2345.2 + dot(uv / 420.0, vec2(0.5, 0.3))) * 0.2 + fract(u_time * 0.1) * 0.3;
        vec3 hueShift3 = vec3(0.2, 0.3, 0.9) * hueShift * 123.2;

        vec3 color = sin(hueShift3) * 0.5 + 0.5;
        color = color * vec3(1.0, 0.25, 1.0 + size);

        star *= sin(u_time * 0.003 + n * 6.2831) * 0.4 + 1.0;
        col += star * size * color;
      }
    }
    
    return col;
  }

  vec2 N(float angle) {
    return vec2(sin(angle), cos(angle));
  }

  void main() {
    vec2 fragCoord = gl_FragCoord.xy;
    vec2 uv = (gl_FragCoord.xy - 0.5 * u_resolution.xy) / u_resolution.y;
    float t = u_time * 0.01;
    
    uv.x = abs(uv.x);
    uv.y += tan((5.0 / 6.0) * 3.1415) * 0.5;

    vec2 n = N((5.0 / 6.0) * 3.1415);
    float d = dot(uv - vec2(0.5, 0.0), n);
    uv -= n * max(0.0, d) * 2.0;

    n = N((2.0 / 3.0) * 3.1415);
    float scale = 1.0;
    uv.x += 1.5 / 1.25;
    for (int i = 0; i < 5; i++) {
      scale *= 1.25;
      uv *= 1.25;
      uv.x -= 1.5;

      uv.x = abs(uv.x);
      uv.x -= 0.5;
      uv -= n * min(0.0, dot(uv, n)) * 2.0;
    }

    uv *= Rot(t);
    vec3 col = vec3(0.0);
    
    for (float i = 0.0; i < 1.0; i += 1.0 / NUM_LAYERS) {
      float depth = fract(i + t);
      float sc = mix(20.0, 0.5, depth);
      float fade = depth * smoothstep(1.0, 0.9, depth);
      col += StarLayer(uv * sc + i * 453.2) * fade;
    }

    gl_FragColor = vec4(col, 1.0);
  }
`
